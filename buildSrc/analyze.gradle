apply plugin: 'checkstyle'
apply plugin: 'java'
apply plugin: 'pmd'
apply plugin: 'jdepend'
apply plugin: "jacoco"
apply plugin: 'com.palantir.jacoco-coverage'

checkstyle {
    ignoreFailures = false
    configProperties.samedir = "${rootProject.projectDir}/buildSrc/checkstyle"
    sourceSets = [sourceSets.main]
    configFile = new File("${rootProject.projectDir}/buildSrc/checkstyle/checkstyle.xml")
}

pmd {
    toolVersion = "6.4.0"
    consoleOutput = true
    sourceSets = [sourceSets.main]
    ruleSetFiles = rootProject.files('buildSrc/pmd/pmd_checks.xml')
}

jdepend {
    toolVersion = "2.9.1"
    sourceSets = [sourceSets.main]
}

jacoco {
    toolVersion = "0.8.1"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination "${buildDir}/jacocoHtml"
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/model/**', '**/entity/**', '**/dto/**', '**/config/**', '**/*Main*/**', '**/roman/**',
                                        '**/factory/**', '**/servlet/**'])
        })
    }
}

jacocoCoverage {
    // Enforce minimum code coverage of 90% for every Java file.
    fileThreshold 0.9
    fileThreshold 0.9, LINE
    fileThreshold 0.9, COMPLEXITY
    fileThreshold 0.9, METHOD
}

check.dependsOn test